*** Settings ***
Documentation       Physical Node Keywords

Library             ../libs/keywords/volume_keywords.py
Library             ../libs/keywords/node_keywords.py
Library             ../libs/keywords/workload_keywords.py

*** Keywords ***
During replica rebuilding, reboot volume node
    reboot_volume_node    ${volume_name}

During replica rebuilding, reboot replica node
    reboot_replica_node    ${volume_name}

Reboot volume ${idx} volume node
    reboot_volume_node    ${volume_list}[${idx}]
    FOR    ${item}    IN    @{volume_list}
        wait for volume_attached    ${item}
    END

Reboot volume ${idx} replica node
    reboot_replica_node    ${volume_list}[${idx}]
    FOR    ${item}    IN    @{volume_list}
        wait for volume_attached    ${item}
    END

Reboot node ${idx}
    power_off_node    ${idx}

Reboot all worker nodes
    power_off_all_worker_nodes

Power off node ${idx} for ${power_off_time_in_min} mins
    power_off_node    ${idx}    ${power_off_time_in_min}

Power off all worker nodes for ${power_off_time_in_min} mins
    power_off_all_worker_nodes    ${power_off_time_in_min}

Wait for longhorn ready
    wait_for_all_instance_manager_running
    FOR    ${deployment}    IN    @{deployment_list}
        wait_for_workload_pod_stable   ${deployment}
    END
    FOR    ${statefulset}    IN    @{statefulset_list}
        wait_for_workload_pod_stable   ${statefulset}
    END

Restart cluster
    reboot_all_nodes
    wait_for_all_instance_manager_running
    FOR    ${deployment}    IN    @{deployment_list}
        wait_for_workload_pod_stable   ${deployment}
    END
    FOR    ${statefulset}    IN    @{statefulset_list}
        wait_for_workload_pod_stable   ${statefulset}
    END
