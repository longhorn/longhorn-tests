def imageName = "${JOB_BASE_NAME}-${env.BUILD_NUMBER}"
def summary

node {
    notifyBuild('STARTED', "")

    checkout scm

    withCredentials([usernamePassword(credentialsId: 'AWS_CREDS', passwordVariable: 'AWS_SECRET_KEY', usernameVariable: 'AWS_ACCESS_KEY')]) {
        stage('build') {
            sh "test_framework/scripts/build.sh"

            sh """ docker run -itd --name ${JOB_BASE_NAME}-${BUILD_NUMBER} \
                              --env LONGHORN_UPGRADE_TEST=${LONGHORN_UPGRADE_TEST} \
                              --env LONGHORN_INFRA_TEST=${LONGHORN_INFRA_TEST} \
                              --env LONGHORN_TEST_CLOUDPROVIDER=${LONGHORN_TEST_CLOUDPROVIDER} \
                              --env TF_VAR_do_token=${env.TF_VAR_do_token} \
                              --env TF_VAR_tf_workspace=${TF_VAR_tf_workspace} \
                              --env LONGHORN_REPO_URL=${LONGHORN_REPO_URL} \
                              --env LONGHORN_REPO_BRANCH=${LONGHORN_REPO_BRANCH} \
                              --env CUSTOM_LONGHORN_MANAGER_IMAGE=${CUSTOM_LONGHORN_MANAGER_IMAGE} \
                              --env CUSTOM_LONGHORN_ENGINE_IMAGE=${CUSTOM_LONGHORN_ENGINE_IMAGE} \
                              --env CUSTOM_LONGHORN_INSTANCE_MANAGER_IMAGE=${CUSTOM_LONGHORN_INSTANCE_MANAGER_IMAGE} \
                              --env TF_VAR_lh_aws_instance_name_controlplane="${JOB_BASE_NAME}-controlplane" \
                              --env TF_VAR_lh_lh_aws_instance_name_worker="${JOB_BASE_NAME}-worker" \
                              --env TF_VAR_lh_aws_access_key=${AWS_ACCESS_KEY} \
                              --env TF_VAR_lh_aws_secret_key=${AWS_SECRET_KEY} \
                              --env TF_VAR_arch=${ARCH} \
                              --env DISTRO=${DISTRO} \
                              --env TF_VAR_distro_version=${DISTRO_VERSION} \
                              --env TF_VAR_lh_aws_instance_type_controlplane=${CONTROLPLANE_INSTANCE_TYPE} \
                              --env TF_VAR_lh_aws_instance_type_worker=${WORKER_INSTANCE_TYPE} \
                              ${imageName}
            """
        }

        try {
            stage ('terraform') {
                sh  " docker exec ${JOB_BASE_NAME}-${BUILD_NUMBER} ${TF_VAR_tf_workspace}/scripts/terraform-setup.sh"
            }

            stage ('longhorn setup & tests') {
                sh  " docker exec ${JOB_BASE_NAME}-${BUILD_NUMBER} ${TF_VAR_tf_workspace}/scripts/longhorn-setup.sh"
            }
            stage ('report generation') {
                sh "docker cp ${JOB_BASE_NAME}-${BUILD_NUMBER}:${TF_VAR_tf_workspace}/longhorn-test-junit-report.xml ."

                if(params.LONGHORN_UPGRADE_TEST) {
                    sh "docker cp ${JOB_BASE_NAME}-${BUILD_NUMBER}:${TF_VAR_tf_workspace}/longhorn-test-upgrade-junit-report.xml ."
                    summary = junit 'longhorn-test-upgrade-junit-report.xml, longhorn-test-junit-report.xml'
                }
                else {
                    summary = junit 'longhorn-test-junit-report.xml'
                }
            }

        } catch(e) {
            currentBuild.result = "FAILED"
            throw e
        } finally {
            stage('releasing resources') {
                sh  " docker exec ${JOB_BASE_NAME}-${BUILD_NUMBER} ${TF_VAR_tf_workspace}/scripts/cleanup.sh"
                sh "docker stop ${JOB_BASE_NAME}-${BUILD_NUMBER}"
                sh "docker rm -v ${JOB_BASE_NAME}-${BUILD_NUMBER}"
                sh "docker rmi ${imageName}"
                if(summary){
                    summary_msg = "\nTest Summary - Failures: ${summary.failCount}, Skipped: ${summary.skipCount}, Passed: ${summary.passCount}  -- Job completed in ${currentBuild.durationString.replace(' and counting', '')}"
                }
                else{
                    summary_msg = "\n Test Failed: No Junit report"
                }

                notifyBuild(currentBuild.result, summary_msg)
            }
        }
    }
}


def notifyBuild(String buildStatus = 'STARTED', String summary_msg) {
  // build status of null means successful
  buildStatus =  buildStatus ?: 'SUCCESSFUL'

  // Default values
  def colorName = 'RED'
  def colorCode = '#FF0000'
  def subject = "${buildStatus}: Job '${env.JOB_BASE_NAME} [${env.BUILD_NUMBER}]'"
  def summary = "${subject} (${env.BUILD_URL})" + summary_msg

  // Override default values based on build status
  if (buildStatus == 'STARTED') {
    color = 'YELLOW'
    colorCode = '#FFFF00'
  } else if (buildStatus == 'SUCCESSFUL') {
    color = 'GREEN'
    colorCode = '#00FF00'
  } else {
    color = 'RED'
    colorCode = '#FF0000'
  }

  // Send notifications
  slackSend (color: colorCode, message: summary, channel: 'CJ4GPBTLG',  teamDomain: 'rancher',  tokenCredentialId: 'longhorn-tests-slack-token')
}
